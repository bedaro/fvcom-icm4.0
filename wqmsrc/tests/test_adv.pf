module test_adv
  use mod_prec, only: SP
  use funit
  use mod_sizes, only: mgl, ngl
  use mod_lims, only: kb, kbm1
  implicit none

contains
    @before
    subroutine set_up()
      use mod_control, only: msr, serial, par
      msr = .True.
      par = .False.
      serial = .True.
    end subroutine set_up

    @after 
    subroutine cleanup()
      use test_grid, only: cleanup_grid
      use mod_wqm, only: wqm_dealloc

      call wqm_dealloc
      call cleanup_grid
    end subroutine cleanup

    @test
    subroutine test_bottom_adv()
      use test_grid, only: build_simple_grid
      use mod_lims, only: numpnt
      use mod_hydrovars, only: uu, vv, wts, d, h, dt, dtfa, dt1, art1
      use mod_wqm, only: wqm_alloc, c1, c2, c2f, dlt, nac, ac
      implicit none
      integer :: i, j
      real(sp) m1, m2

      call build_simple_grid
      call wqm_alloc

      ! Define velocity field based on some FVCOM output
      ! Generated by running the test grid with a constant west wind,
      ! no density gradient
      uu(:,1) = (/  2.9475e-2 ,  4.0985e-2 ,  4.1687e-2 , -6.9795e-10, &
                    4.2111e-2 , -7.4168e-10,  4.0793e-2 ,  4.0919e-2 , &
                    4.3331e-2 ,  3.2084e-2 ,  6.9790e-10,  4.2112e-2 , &
                    7.4173e-10,  2.9476e-2 ,  4.0987e-2 ,  4.1688e-2 /)
      uu(:,2) = (/ -1.2158e-3 ,  5.8605e-3 ,  1.0754e-2 , -7.1716e-10, &
                    6.7697e-3 , -7.1092e-10,  1.0451e-2 ,  1.0219e-2 , &
                    5.8193e-3 ,  5.6382e-3 ,  7.1711e-10,  6.7707e-3 , &
                    7.1097e-10, -1.2156e-3 ,  5.8619e-3 ,  1.0755e-2 /)
      uu(:,3) = (/ -4.7834e-2 , -4.4815e-2 , -3.9882e-2 , -9.1830e-10, &
                   -5.1430e-2 , -4.4381e-10, -3.1671e-2 , -4.8318e-2 , &
                   -4.3922e-02, -5.0282e-02,  9.1829e-10, -5.1432e-02, &
                    4.4386e-10, -4.7834e-02, -4.4814e-02, -3.9883e-02 /)
      vv(:,1) = (/  4.8022e-09, -5.5612e-03,  6.7917e-09,  5.8548e-03, &
                    3.8780e-04,  4.9119e-03,  5.6692e-07,  1.1778e-07, &
                   -4.6264e-07, -2.6822e-07, -5.8544e-03, -3.8822e-04, &
                   -4.9122e-03,  2.2254e-09,  5.5612e-03,  3.1474e-09 /)
      vv(:,2) = (/ -1.9808e-10, -5.7609e-03,  1.7520e-09,  6.0159e-03, &
                    4.1619e-04,  4.7082e-03,  5.7507e-07,  1.9512e-07, &
                   -3.9201e-07, -3.3516e-07, -6.0155e-03, -4.1643e-04, &
                   -4.7085e-03, -9.1770e-11,  5.7609e-03,  8.1198e-10 /)
      vv(:,3) = (/ -7.7931e-09, -4.7447e-03, -6.4976e-09,  7.7033e-03, &
                    4.0068e-04,  2.9392e-03,  3.1880e-07,  4.1640e-07, &
                   -5.5637e-08, -4.3840e-07, -7.7032e-03, -4.0028e-04, &
                   -2.9395e-03, -3.6114e-09,  4.7448e-03, -3.0111e-09 /)
      wts(:,1) = 0.
      wts(:,2) = (/ -3.5331e-04, -7.2538e-06,  3.6775e-04, -2.9723e-04, &
                     2.9465e-04, -4.6586e-04, -1.6352e-05,  4.9326e-04, &
                    -2.9724e-04,  2.9465e-04, -3.5331e-04, -7.2558e-06, &
                     3.6775e-04 /)
      wts(:,3) = (/ -4.0131e-04, -1.0238e-05,  4.2820e-04, -3.5915e-04, &
                     3.1677e-04, -5.2500e-04,  3.5968e-05,  6.3121e-04, &
                    -3.5916e-04,  3.1677e-04, -4.0131e-04, -1.0246e-05, &
                     4.2822e-04 /)

      ! Define timestep and initial tracer concentration
      ! in bottom cells
      numpnt = 0
      dlt = 1
      nac = 1
      ac(1) = 12
      c2(:, 3, 12) = 1.
      d = h
      dt = d
      dtfa = d ! surface heights were negligible in the solution
      dt1 = d(1)

      call viscof_h

      ! Advect once
      call adv_wqm
      !print 10, "C2", ((c2(i,j,12), i=1, mgl), j=1, kbm1)
      !print 10, "C2F", ((c2f(i,j,12), i=1, mgl), j=1, kbm1)

      ! Add up total system mass before and after
      m1 = 0.
      m2 = 0.
      do i = 1, mgl
        do j = 1, kbm1
          m1 = m1 + c2(i,j,12) * art1(i)
          m2 = m2 + c2f(i,j,12) * art1(i)
        end do
      end do

      ! Tests
      ! mass conservation
      @assertRelativelyEqual(m1, m2, message="Mass cons", tolerance=1e-4)
      ! No tracer in upstream middle cells
      @assertEqual(0., c2f(5, 2, 12), message="Upstream clean")
      @assertEqual(0., c2f(8, 2, 12), message="Upstream clean")
      @assertEqual(0., c2f(10, 2, 12), message="Upstream clean")
      ! tracer in downstream middle cells
      @assertTrue(c2f(4, 2, 12) > 0., message="Downstream dirty")
      @assertTrue(c2f(6, 2, 12) > 0., message="Downstream dirty")
      @assertTrue(c2f(9, 2, 12) > 0., message="Downstream dirty")

10    format(a8, 6(e12.5))
    end subroutine test_bottom_adv

end module test_adv
